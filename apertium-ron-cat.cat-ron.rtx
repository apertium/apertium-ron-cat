!!! Recusive transfer for Catalan->Romanian

!!! Attribute categories

gender = (GD m) m f nt mn fn @mf @mfn GD ;              ! Gender for dix entries except clitics. <GD> is replaced by <m> by default. <mf> and <mfn> are never replaced.
gender_chunk = (GD m) m f nt GD ;                       ! Gender for chunks. <GD> is replaced by <m> by default.
gender_clitic = m f nt mn fn mf mfn ;                   ! Gender for clitics.
number = (ND sg) sg pl @sp ND ;                         ! Number for dix entries except clitics. <ND> is replaced by <sg> by default.
number_clitic = sg pl sp ;                              ! Number for clitics.
case = nom dg ;                                         ! Case for nouns and adjectives (nominative, dative/genitive)
definite = ind def ;                                    ! Definite article for nouns and adjectives
person = (PD p3) p1 p2 p3 PD ;                          ! Person for verbs and clitics
tense = pri fti cni imp prs pii ifi pis inf ger pp ;    ! Tense for verbs

acr = acr ;                                             ! Optional tag for nouns which are acronyms.
adj_type = def dem ind ord pos ;                        ! Optional tag for adjectives with a type.
adv_type = itg ;                                        ! Optional tag for adverbs with a type.
gen = gen ;                                             ! Optional tag for prepositions which require the following noun in dative/genitive.
np_type = ant cog top al ;                              ! Type of proper noun.
prn_clitic = pro enc ;                                  ! Type of clitic pronoun.
prn_refadv = ref adv ;
prn_type = tn itg an nn ;                               ! Type of pronoun.
prn_type2 = pos ;                                       ! Second type of pronoun.
vb_type = vbavea vblex vbmod vbser ;                    ! Type of verb.
vb_cnj = cnj impers ;                                   ! Conjugation status of a verb (conjugated form, impersonal form).

SN_det = undet det predet nodet prnpers ;               ! Determiner status of a SN.
SPrep_type = pr SN ;                                    ! Type of SPrep (only preposition, preposition + SN).



!!! Tag rewrite rules

gender > gender_chunk: mf GD, mfn GD, mn m, fn f ;
gender_chunk > gender: GD GD ;



!!! Output patterns

adj:        (if((1.gender_chunk=m | 1.gender_chunk=nt | 1.gender=m | 1.gender=nt) & 1.number=sg) 1(adj_dix)[gender=mn]
            elif((1.gender_chunk=f | 1.gender_chunk=nt | 1.gender=f | 1.gender=nt) & 1.number=pl) 1(adj_dix)[gender=fn]
            else 1(adj_dix)) ;
adj_dix:    _.adj_type.gender.number.case.definite ;
adv:        _.adv_type ;
det:        (if((1.gender_chunk=m | 1.gender_chunk=nt) & 1.number=sg) 1(det_dix)[gender=mn]
            elif((1.gender_chunk=f | 1.gender_chunk=nt) & 1.number=pl) 1(det_dix)[gender=fn]
            else 1(det_dix)) ;
det_dix:    _.adj_type.gender.number.case ;
n:          _.acr.gender.number.case.definite ;
np:         _.np_type.gender.number.case.definite ;
num:        (if((1.gender_chunk=m | 1.gender_chunk=nt) & 1.number=sg) 1(num_dix)[gender=mn]
            elif((1.gender_chunk=f | 1.gender_chunk=nt) & 1.number=pl) 1(num_dix)[gender=fn]
            else 1(num_dix) ) ;
num_dix:    _.gender.number ;
num_digits: _ ;
pr:         _.gen ;
preadv:     _ ;
predet:     _.adj_type.gender.number.case ;
prn:        _.prn_type.prn_type2.gender.number.case ;
prnclt:     _.prn_clitic.prn_refadv.person.gender_clitic.number_clitic.case ;
prnpers:    _.prn_type.person.gender.number.case ;
rel:        _.prn_type.gender.number.case ;
vbavea:     _.tense.person.number ;
vbhaver:    _.tense.person.number ;
vblex:      _.tense.person.number ;
vbmod:      _.tense.person.number ;
vbser:      _.tense.person.number ;
vb_impers:  _.tense ;
vb_pp:      (if((1.gender_chunk=m | 1.gender_chunk=nt) & 1.number=sg) 1(vb_pp_dix)[gender=mn]
            elif((1.gender_chunk=f | 1.gender_chunk=nt) & 1.number=pl) 1(vb_pp_dix)[gender=fn]
            else 1(vb_pp_dix) ) ;
vb_pp_dix:  _.tense.gender.number ;

Clt:        _.prn_clitic.prn_refadv.person.gender_clitic.number_clitic.case ;
PV:         _.person.gender_chunk.number ;
SAdj:       _.adj_type.gender_chunk.number.case.definite ;
SAdv:       _.adv_type ;
SDet:       _.adj_type.gender_chunk.number.case ;
SN:         _.SN_det.np_type.person.gender_chunk.number.case.definite ;
SNum:       _.gender_chunk.number ;
SPredet:    _.adj_type.gender_chunk.number.case ;
SPrep:      _.SPrep_type.gen ;
SV:         _.vb_type.vb_cnj.tense.person.gender_chunk.number ;



!!! Lexical lists

adjord_nodet = prim întâi I ;
num_de = zero unu doi trei patru cinci șase șapte opt nouă zece
         unsprezece doisprezece treisprezece paisprezece cincisprezece șaisprezece șaptesprezece optsprezece nouăsprezece
         0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 ;
elimina_clt = li em ens ;



!!! Reduction rules

Clt ->  1: %prn.[prn_clitic]
                [$lem=1.lem,/sl=1[lem=1.lem/sl]]
                { %1(prnclt)[lem=$lem] } |
        2: %prn.[prn_clitic].adv [$lem=1.lem] {} |
        
        2: %Clt Clt ?(1.prn_clitic=2.prn_clitic)
                { (if($prn_clitic=pro) [%1[case=dg] _ 2[prn_clitic=pro]] else [%1[case=dg] + 2])  } |

        3: %Clt Clt ?(1.prn_clitic=2.prn_clitic & 2.lem/sl incl elimina_clt)  ! Si el segon pronom és elimina_clt, desapareix
                { %1 } |
        4: Clt.[prn_clitic].ref %Clt ?(1.prn_clitic=2.prn_clitic)  ! Si el primer pronom és ref, desapareix
                { %2 } |
        5: %Clt Clt.[prn_clitic].adv ?(1.prn_clitic=2.prn_clitic)  ! Si el segon pronom és adv, desapareix i no hi ha el canvi a datiu
                { %1 } ;


SAdj -> 1: %adj
                [$adj_type="",$case=nom,$definite=ind]
                { %1[adj_type=""] } |
        2: %antepenúltim@adj
                [$adj_type="ord",$case=nom,$definite=ind]
                { %1 } |
        2: %penúltim@adj
                [$adj_type="ord",$case=nom,$definite=ind]
                { %1 } |
        2: %últim@adj
                [$adj_type="ord",$case=nom,$definite=ind]
                { %1 } |
        2: %adj.ord
                [$adj_type="ord",$case=nom,$definite=ind]
                { (if($lem incl adjord_nodet)
                        %1
                elif(($gender_chunk=m | $gender_chunk=nt) & $number=sg)
                        [al@det.def.mn.$number _ %1[case=nom,definite=ind]]
                elif(($gender_chunk=f | $gender_chunk=nt) & $number=pl)
                        [al@det.def.fn.$number _ %1[case=nom,definite=ind]]
                else [al@det.def.$gender.$number _ %1[case=nom,definite=ind]] ) } |
        2: %num.[gender].[number] ?(1.pos_tag/tl=adj)
                [$case=nom,$definite=ind]
                { (if($lem incl adjord_nodet)
                        %1
                elif(($gender_chunk=m | $gender_chunk=nt) & $number=sg)
                        [al@det.def.mn.$number _ %1[case=nom,definite=ind]]
                elif(($gender_chunk=f | $gender_chunk=nt) & $number=pl)
                        [al@det.def.fn.$number _ %1[case=nom,definite=ind]]
                else [al@det.def.$gender.$number _ %1[case=nom,definite=ind]] ) } |

        1: SAdv %SAdj
                { 1 _ %2 } |

        0: %SV.*.impers.pp
                { %1 } ;


SAdv -> 1: %adv
                { 1 } |
        1: %preadv
                { 1 } |

        1: el@det.def.m.sg %num
                { în@pr _ 2(num_digits) } |
        1: en@pr %num
                { 1 _ 2(num_digits) } ;


SDet -> 1: %det.def
                [$case=nom]
                { %1 } |
        2: %"l'altre"@det.def
                [$case=nom,$adj_type=ind]
                { %1(det_dix)[adj_type=ind,definite=def] } |
        1: %det.ind
                [$case=nom,$adj_type=ind]
                { %1[adj_type=ind] } |
        1: %det.pos
                [$case=nom]
                { %1(adj)[definite=ind] } |
        2: %"el seu"@det.pos
                [$case=nom,$gender_chunk=1.gender/sl,$number=1.number/sl]
                { 1(adj)[gender=1.gender/ref,number=1.number/ref,case=nom,definite=ind] } |
        2: %adj.pos
                [$case=nom]
                { %1[definite=ind] } |
        3: %seu@adj.pos
                [$case=nom,$gender_chunk=1.gender/sl,$number=1.number/sl]
                { 1[gender=1.gender/ref,number=1.number/ref,case=nom,definite=ind] } |
        1: %det.dem
                [$case=nom]
                { %1} |
        2: %adj.ind
                [$adj_type=ind,$case=nom]
                { %1[adj_type=ind,definite=ind] } |
        2: %bastant@adj
                [$adj_type="ind",$case=nom]
                { %1[adj_type=ind,definite=ind] } |
        2: %força@adj
                [$adj_type="ind",$case=nom]
                { %1[adj_type=ind,definite=ind] } |
        2: %més@adj
                [$adj_type="ind",$case=nom]
                { %1[adj_type=ind] } |
        2: %molt@adj
                [$adj_type="ind",$case=nom]
                { %1[adj_type=ind,definite=ind] } ;


SPredet -> 1: %predet
                [$case=nom]
                { %1 } ;


SPrep -> 0: %pr SN
                [$SPrep_type=SN]
                { 1 _ 2[definite=(if(1.lem=cu) def else ind)] } | ! Pot ser def sense cu si té complements
         1: %pr.gen SN
                [$SPrep_type=SN]
                { 1 _ 2[case=dg] } |
         1: %de@pr SN
                [$SPrep_type=SN]
                { 2[case=dg] } ;


SN ->   1: %n
                [$SN_det=undet,$person=p3,$case=nom,$definite=ind]
                { %1 } |

        1: %np.ant.m
                [$SN_det=det,$case=nom,$definite=def]
                { (if($case=dg) [lui@adj.pos.mn.sg.nom.def _] ) %1[case=nom,definite=def] } |
        1: %np.ant.f
                [$SN_det=det,$case=nom,$definite=def]
                { (if(1.gen not=gen & $case=dg) [lui@adj.pos.mn.sg.nom.def _ %1[case=nom,definite=def]] else %1[definite=def] ) } |
        1: %np.cog
                [$SN_det=det,$case=nom,$definite=def]
                { (if($case=dg) [lui@adj.pos.mn.sg.nom.def _] ) %1[case=nom,definite=def] } |
        1: %SN.det.ant SN.det.cog
                { %1 _ 2 } |
        1: %np.top
                [$SN_det=det,$case=nom,$definite=def]
                { %1[definite=def] } |
        1: %np.al
                [$SN_det=undet,$case=nom,$definite=ind]
                { %1 } |

        2: %prn.tn.[person] !Compte amb el datiu, que crea un error de generació
                [$SN_det=prnpers,$case=nom]
                { %1(prnpers) } |
        1: %prn.tn
                [$SN_det=nodet,$case=nom]
                { %1 } |
        1: %prn.itg
                [$SN_det=nodet,$case=nom]
                { %1 } |

        2: %SN SPrep
                { %1 _ 2 } |

        2: %SN SAdj
                [$gender_chunk=(if(1.gender=GD)2.gender else 1.gender),$number=(if(1.number=ND)2.number else 1.number)]
                { %1 _ 2[gender=$gender,number=$number] } |
        2: SAdj.ord %SN
                [$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { %1 _ 2[gender=$gender,number=$number] } |
        3: SAdj.ord %SN ?(1.lem/tl=întâi)
                [$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { 2[gender=$gender,number=$number,definite=def] _ %1 } |

        1: SDet.def %SN.undet
                [$SN_det=det,$definite=def,$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { %2 } |
        1: SDet.def %SN.det.ant
                { %2 } |
        1: SDet.dem %SN.undet
                [$SN_det=det,$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { %1[gender=$gender,number=$number] _ 2 } |
        1: SDet.ind %SN.undet
                [$SN_det=det,$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { %1[gender=$gender,number=$number] _ 2 } |
        3: SDet.pos %SN.undet
                [$SN_det=det,$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { %2[definite=def] _ 1[gender=$gender,number=$number] } |
        3: SDet.pos %SN.undet SPrep
                [$SN_det=det,$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { %2[definite=def] _ 1[gender=$gender,number=$number] _ 3 } |
        3: %SN.undet SDet.pos 
                [$SN_det=det,$gender_chunk=(if(1.gender=GD)2.gender else 1.gender),$number=(if(1.number=ND)2.number else 1.number)]
                { %1[definite=def] _ 2[gender=$gender,number=$number] } |

        1: SPredet.def %SN.det
                [$SN_det=predet,$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { %1 _ %2[case=nom,definite=def] } |

        1: SNum %SN.undet
                [$gender_chunk=(if(2.gender=GD)1.gender else 2.gender),$number=(if(2.number=ND)1.number else 2.number)]
                { 1[gender=$gender] (if(1.lem not incl num_de)[_ de@pr]) _ %2 } |

        2: %SN que@rel SV ! Per fer la concordança del verb de la hem de saber si el verb és transitiu o intrasitiu
                { %1 _ 2 _ 3 } ;


SNum ->  0: %num
                { %1(num_digits) } |
         1: %num.[gender]
                { %1 } |
         2: SNum %SNum
                { 1 _ %2 } ;


SV ->   1: %vblex
                [$vb_cnj=cnj]
                { %1 } |
        2: %vblex.fti
                [$vb_cnj=cnj]
                { vrea@vaux.pri.$person.$number _ 1(vb_impers)[tense=inf] } |
        2: %vblex.cni
                [$vb_cnj=cnj]
                { avea@vbavea.$tense.$person.$number _ 1(vb_impers)[tense=inf] } |
        2: %vblex.pis
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |
        2: %vblex.imp
                [$vb_cnj=cnj,$person=(if(1.person=p3 & 1.number=pl) p2 else 1.person)] ! cantin > cântați
                { %1 } |

        2: %vblex.inf
                [$vb_cnj=impers]
                { (if($tense=pp) %1(vb_pp) else %1(vb_impers)) } |
        2: %vblex.ger
                [$vb_cnj=impers]
                { %1(vb_impers) } |
        2: %vblex.pp
                [$vb_cnj=impers]
                { %1(vb_pp)[gender=$gender_chunk,number=$number] } |

        1: %vbser
                [$vb_cnj=cnj]
                { %1 } |
        2: %vbser.fti
                [$vb_cnj=cnj]
                { vrea@vaux.pri.$person.$number _ 1(vb_impers)[tense=inf] } |
        2: %vbser.cni
                [$vb_cnj=cnj]
                { avea@vbavea.$tense.$person.$number _ 1(vb_impers)[tense=inf] } |
        2: %vbser.pis
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |
        2: %vbser.imp
                [$vb_cnj=cnj,$person=(if(1.person=p3 & 1.number=pl) p2 else 1.person)] ! cantin > cântați
                { %1 } |

        2: %vbser.inf
                [$vb_cnj=impers]
                { (if($tense=pp) %1(vb_pp) else %1(vb_impers)) } |
        2: %vbser.ger
                [$vb_cnj=impers]
                { %1(vb_impers) } |
        2: %vbser.pp
                [$vb_cnj=impers]
                { %1(vb_pp) } |

        1: %vbhaver
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |
        2: %vbhaver.fti
                [$vb_cnj=cnj]
                { vrea@vaux.pri.$person.$number _ fi@vbser.inf } |
        2: %vbhaver.cni
                [$vb_cnj=cnj]
                { %1 } |
        2: %vbhaver.pis
                [$vb_cnj=cnj,$tense=cni]
                { %1 } |

        2: %vbhaver.inf
                [$vb_cnj=impers]
                { %1(vb_impers) } |
        2: %vbhaver.ger
                [$vb_cnj=impers,$tense=inf]
                { %1(vb_impers) } |
        2: %vbhaver.pp
                [$vb_cnj=impers,$tense=inf]
                { %1(vb_impers) } |

        1: %vbmod
                [$vb_cnj=cnj]
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |
        2: %vbmod.fti
                [$vb_cnj=cnj]
                { vrea@vaux.pri.$person.$number _ 1(vb_impers)[tense=inf] } |
        2: %vbmod.cni
                [$vb_cnj=cnj]
                { avea@vbavea.$tense.$person.$number _ 1(vb_impers)[tense=inf] } |
        2: %vbmod.pis
                [$vb_cnj=cnj,$tense=pri]
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |
        2: %vbmod.imp
                [$vb_cnj=cnj,$person=(if(1.person=p3 & 1.number=pl) p2 else 1.person)] ! cantin > cântați
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |

        2: %vbmod.inf
                [$vb_cnj=impers]
                { (if($tense=pp) %1(vb_pp) else %1(vb_impers)) } |
        2: %vbmod.ger
                [$vb_cnj=impers]
                { %1(vb_impers) } |
        2: %vbmod.pp
                [$vb_cnj=impers]
                { %1(vb_pp) } |

        2: %merge@SV.vblex.cnj.pri SV.*.impers.inf
                [$lem=2.lem,$tense=ifi]
                { avea@vbavea.pri.$person.$number _ 2[tense=pp] } |
        2: %avea@SV.vbavea.cnj.pri SV.*.impers.pp
                [$lem=2.lem,$tense=ifi]
                { 1[person=$person,number=$number] _ 2 } |
        2: %avea@SV.vbavea.cnj.cni SV.*.impers.pp
                [$lem=2.lem,$tense=pis]
                { 1[person=$person,number=$number] _ fi@vbser.inf _ 2 } |


        1: %SV.*.impers.ger Clt.enc
                { %1 + 2 } |

        1: %SV.*.impers.inf Clt.enc
                { 2[prn_clitic=pro] _ %1 } |

        1: %SV.*.cnj.imp Clt.enc
                { %1 + 2 } |
        2: %SV.*.cnj.imp.p2 Clt.enc.ref
                { %1 + 2[lem="vă",prn_refadv="",person=p2,gender_clitic=mf,number_clitic=pl] } |

        1: %SV.*.cnj.ifi Clt.enc
                { 2[prn_clitic=pro] _ %1 } |

        1: Clt.pro %SV.*.cnj
                { 1 _ %2 } |

        1: SAdv %SV
                { 1 _ %2 } |

        2: %SV.*.cnj SAdj.$gender
                { %1 _ %2 };


PV ->   0: SN.$person SV.*.cnj
                [$gender_chunk=(if(1.gender=GD)2.gender else 1.gender),$number=(if(1.number=ND)2.number else 1.number)]
                { %1 _ %2 } ;
